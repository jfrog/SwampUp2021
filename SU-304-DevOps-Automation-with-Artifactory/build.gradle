plugins {
    id 'application'
    id 'maven-publish'
    id 'com.jfrog.artifactory' version '4.18.3'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

application {
    mainClass = 'com.jfrog.swampup.devsecops.HelloWorld'
}

group='com.jfrog.swampup.devsecops'
version="${projectVersion}"

repositories {
    maven {
        url "${artifactoryUrl}/${artifactoryGradleRepo}"
        credentials {
            username = "${artifactoryUser}"
            password = "${artifactoryApiKey}"
        }
    }
}

configurations.archives.artifacts.removeAll {
    // exclude from the archives configuration all artifacts that were generated by distZip
    def depTasks = it.getBuildDependencies().getDependencies()
    depTasks.contains(distZip)
}

artifactory {
    publish {
        contextUrl = "${artifactoryUrl}"
        repository {
            repoKey = "${artifactoryGradleRepo}"
            username = "${artifactoryUser}"
            password = "${artifactoryApiKey}"
        }
        defaults {
            publications ('mavenJava')
            publishConfigs('archives')
            properties = ['build.status': 'dev', 'build.os': 'win32, deb, osx']
            publishBuildInfo = false
            publishIvy = false
        }
    }
}

dependencies {
    implementation group: 'org.apache.struts', name: 'struts2-core', version: "${strutsVersion}"
    implementation group: 'commons-io', name: 'commons-io', version: '2.8.0'
    implementation group: 'org.javassist', name: 'javassist', version: '3.20.0-GA'
}
